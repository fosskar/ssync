# nixbot effects. The GitToken comes from nixbot at runtime (a
# github app installation token on github repos).
{ pkgs }:
let
  inherit ((pkgs.lib.importTOML ../Cargo.toml).workspace.package) version;
in
{ primaryRepo, ... }:
{
  # Auto-release on version bump: push effects run only after the whole build
  # (nix flake check) succeeded on that commit, so a broken bump can never
  # become a release. No-op while the workspace version is already tagged.
  onPush.default.outputs.effects = pkgs.lib.optionalAttrs (primaryRepo.branch or null == "main") {
    release =
      pkgs.runCommand "effect-release"
        {
          nativeBuildInputs = [
            pkgs.cacert
            pkgs.gh
            pkgs.jq
          ];
          secretsMap = builtins.toJSON { git.type = "GitToken"; };
          HOME = "/build";
        }
        ''
          set -euo pipefail
          GH_TOKEN=$(jq -re '.git.data.token' "$HERCULES_CI_SECRETS_JSON")
          export GH_TOKEN

          if gh api "repos/fosskar/ssync/git/ref/tags/v${version}" > /dev/null 2>&1; then
            echo "v${version} already released"
          else
            gh release create "v${version}" --repo fosskar/ssync \
              --generate-notes --target "${primaryRepo.rev}"
          fi
        '';
  };

  onSchedule.update-flake-inputs = {
    when = {
      hour = 5;
      minute = 0;
    };
    outputs.effects.update-flake-inputs =
      pkgs.runCommand "effect-update-flake-inputs"
        {
          nativeBuildInputs = [
            pkgs.cacert
            pkgs.git
            pkgs.jq
            pkgs.nix
          ];
          # The GitToken is a github app installation token, so it serves the
          # direct github API calls too.
          secretsMap = builtins.toJSON { git.type = "GitToken"; };
          HOME = "/build";
        }
        ''
          set -euo pipefail
          token=$(jq -re '.git.data.token' "$HERCULES_CI_SECRETS_JSON")
          export FORGE_TOKEN="$token"
          export GITHUB_TOKEN="$token"
          export NIX_CONFIG="experimental-features = nix-command flakes
          access-tokens = github.com=$token"

          git config --global user.name 'fosskar[bot]'
          git config --global user.email '300917551+fosskar[bot]@users.noreply.github.com'
          git config --global safe.directory '*'

          git clone --depth 1 --progress \
            "https://oauth2:$token@github.com/fosskar/ssync.git" repo
          cd repo

          nix run "github:fosskar/nixfiles#updater-flake-inputs"
        '';
  };
}
